-- Copyright (c) 2021  Yoshinao Isobe
-- National Institute of Advanced Industrial Science and Technology (AIST)
-- 2021/09/27

-- -----------------------------------
-- Buffer type
-- -----------------------------------

-- datatype Buff_Policy = RTC_blk | RTC_ovw

-- -----------------------------------
-- channel
-- -----------------------------------

-- RTC_max must be defiend by designers

RTC_id = {1..RTC_max}

channel tock
channel in,out : RTC_ev
channel ch  : RTC_id.RTC_ev
channel obs : RTC_msg
channel ow : RTC_id     -- overwritten

-- -----------------------------------
-- composition of RTCs
-- -----------------------------------

external prioritise
-- transparent wbisim

RTM_compo(FSM, link, c, w)
 = let

  Buff(i) = let
   Rcv = {e | k <- RTC_id, e <- link(k,i)}
   Var(s)
    =    (#s > 0) & out!head(s) -> Var(tail(s))
      [] (#s < c) & in?e:Rcv -> Var(s^<e>)
      [] ((#s == c) and w) & in?e:Rcv -> ow.i -> Var(tail(s)^<e>)
  within Var(<>)

--      [] (#s < c(i)) & in?e:Rcv -> Var(s^<e>)
--      [] ((#s == c(i)) and w(i)) & in?e:Rcv -> ow.i -> Var(tail(s)^<e>)

  RTC(i) = let
    Snd = {(j,e) | j <- RTC_id, e <-link(j,i)}
    RFSM = FSM(i)[[out.e <- ch.i.e | e <- RTC_ev]]
    RBuff = Buff(i)[[in.e <- ch.j.e | (j,e) <- Snd]][[out <- in]]
  within (RFSM [|{|in|}|] RBuff) \ {|in|}

  Chs(i) = let
    rlink(i,j) = if (i==j) then RTC_ev else link(i,j)
  within {tock, ch.k.e | k <- RTC_id, j <- {1..i-1},
                         e <- inter(rlink(k,i), rlink(k,j))}

  Sys = Compo(RTC_max, Chs, RTC) \ {|ch|}

    Compo(i,X,P)
     = if (i==1) then P(1) else Compo(i-1,X,P) [|X(i)|] P(i)

 within
  prioritise(Sys, <{|obs,ow|}, {tock}>)


