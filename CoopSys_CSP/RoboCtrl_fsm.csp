-- Copyright (c) 2021  Yoshinao Isobe
-- National Institute of Advanced Industrial Science and Technology (AIST)
-- 2021/09/27

RoboCtrl = let

-- -----------------------------------
-- Definition of FSMs
-- -----------------------------------

  Pos(Goal) = PosGoal
  Pos(Home) = PosHome
  Pos(_) = -1 -- undef

  ChkPrm(pg,p) =if (pg<p) then Box else Prm

  Vel(p,d,s) = if (d==Box) then VelS(s) else VelP(p,Pos(d))
     VelS(s) = if (Thr<s) then 0 else V
    VelP(p,p') = if (p==p') then 0 else if (p<p') then V else -V

  Bat(b,p)
   = if (b==Full and p==PosHome) then {Full}
     else if (b==Low and p!=PosHome) then {Low}
     else {Low,Full}

  -- ---------------------
  -- State: Ctrl
  -- ---------------------

  Ctrl(pos,dst,bat,arv,chg,sns) = let

    Init
    = Select(pos,dst,bat,arv,chg,sns)
  
    Select(pos,dst,bat,arv,chg,sns)
    = in.dest?dst
        -> out.velocity!Vel(pos,dst,sns)
        -> Select(pos,dst,bat,True,chg,sns)
      []
      in.position?pos
        -> (if (arv and (dst!=Box) and (pos==Pos(dst)))
            then (out.arrive!dst
                  -> out.velocity!0
                  -> (if (dst==Home)
                      then Select(pos,dst,bat,False,True,sns) -- arrive Home
                      else Select(pos,dst,bat,False,chg,sns)))
            else Select(pos,dst,bat,arv,chg,sns))
      []
      in.sensor?sns
        -> (if (sns==Thr and dst==Box and arv)
            then (out.arrive!ChkPrm(PosGoal,pos)
                     -> out.velocity!0
                     -> Select(pos,dst,bat,False,chg,sns))
            else Select(pos,dst,bat,arv,chg,sns))
      []
      tock
        -> |~| bat:Bat(bat,pos) @
                  if ((not chg) and (bat == Low ))
                  then (out.low  -> Select(pos,dst,bat,arv,not chg,sns))
             else if (chg and (bat == Full))
                  then (out.full -> Select(pos,dst,bat,arv,not chg,sns))
             else Select(pos,dst,bat,arv,chg,sns)

  within Init

  -- ---------------------
  -- State: Start
  -- ---------------------

  Start = let
  
    Init
    = in.start -> Ctrl(0,Home,Full,False,True,0)

  within Init

within out.pose!0 -> out.velocity!0 -> Start

